plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

group = 'com.github.alexdev03'
version = '1.0'

repositories {
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url "https://repo.purpurmc.org/snapshots"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven { url = 'https://repo.minebench.de/' }
    maven { url = 'https:/oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://repo.opencollab.dev/maven-public/' }
    maven { url = 'https://repo.codemc.io/repository/maven-releases/' }
    maven { url = 'https://repo.viaversion.com/' }
    maven { url = 'https://repo.opencollab.dev/main/' }

}

dependencies {
    compileOnly 'com.github.gabber235:typewriter:ee4fc95cce'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.0-RC'
    compileOnly 'com.github.dyam0:LirandAPI:96cc59d4fb'
    compileOnly 'net.kyori:adventure-api:4.13.1'
    compileOnly 'net.kyori:adventure-text-minimessage:4.13.1'
    compileOnly 'org.purpurmc.purpur:purpur-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'io.github.miniplaceholders:miniplaceholders-api:2.1.0'
    compileOnly 'net.kyori:adventure-api:4.8.1'
    compileOnly('org.geysermc.floodgate:api:2.2.2-SNAPSHOT')

    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'com.github.Exlll.ConfigLib:configlib-paper:dev-SNAPSHOT'
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'de.themoep:minedown-adventure:1.7.2-SNAPSHOT'
    implementation 'com.github.alexdev03:drink:1.5.1'
    implementation 'com.github.Tofaa2:EntityLib:v1.2.1-SNAPSHOT'
    implementation 'com.github.retrooper.packetevents:spigot:2.2.0'
}


shadowJar {
    relocate 'org.jetbrains', 'org.alexdev.libraries.jetbrains'
    relocate 'org.intellij', 'org.alexdev.libraries.intellij'
    relocate 'de.themoep', 'org.alexdev.libraries.themoep'
    relocate 'me.tofaa.entitylib', 'org.alexdev.libraries.entitylib'
    relocate 'com.github.retrooper.packetevents', 'org.alexdev.libraries.packetevents.api'
    relocate 'io.github.retrooper.packetevents', 'org.alexdev.libraries.packetevents.impl'
    relocate 'com.jonahseguin.drink', 'org.alexdev.libraries.drink'
    relocate 'de.exlll.configlib', 'org.alexdev.libraries.configlib'
    relocate 'net.kyori', 'org.alexdev.libraries.kyori'
    relocate 'javax.annotation', 'org.alexdev.libraries.annotation'

    dependencies {
        //noinspection GroovyAssignabilityCheck
        exclude dependency(':slf4j-api')
        //gson
        exclude dependency('com.google.code.gson:gson')
        //kyori all submodules

    }

    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')

    minimize()
}
build.dependsOn shadowJar
clean.delete "$rootDir/target"

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
