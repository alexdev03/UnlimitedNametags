plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.5"
    id 'maven-publish'
    id 'xyz.jpenilla.run-paper' version '2.3.0'
}

group = 'com.github.alexdev03'

repositories {

    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = 'https://repo.extendedclip.com/releases'
    }
    maven { url = 'https://repo.minebench.de/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://repo.opencollab.dev/maven-public/' }
    maven { url = 'https://repo.codemc.io/repository/maven-releases/' }
    maven { url = 'https://repo.viaversion.com/' }
    maven { url = 'https://repo.opencollab.dev/main/' }
    mavenLocal()
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
    maven { url = "https://repo.oraxen.com/releases" }
    maven { url = "https://repo.oraxen.com/snapshots" }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven { url = "https://repo.viaversion.com" }
    maven { url = "https://maven.evokegames.gg/snapshots" }
    maven { url = 'https://repo.alessiodp.com/releases' }
    maven { url = 'https://maven.typewritermc.com/beta' }
    maven { url = 'https://repo.nexomc.com/snapshots/' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperApiVersion}"
    compileOnly "net.kyori:adventure-api:${adventureApiVersion}"
    compileOnly("com.typewritermc:engine-paper:${typeWriterVersion}") {
        exclude group: 'io.papermc.paper' // Exclude Paper API
        exclude group: 'com.github.Tofaa2.EntityLib'
    }
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "me.clip:placeholderapi:${placeholderapiVersion}"
    compileOnly "io.github.miniplaceholders:miniplaceholders-api:${miniplaceholdersApiVersion}"
    compileOnly "net.kyori:adventure-api:${adventureApiVersion}"
    compileOnly "org.geysermc.floodgate:api:${floodgateApiVersion}"
    compileOnly "de.exlll:configlib-yaml:${configlibVersion}"
    compileOnly "de.exlll:configlib-paper:${configlibVersion}"
    compileOnly "com.github.retrooper:packetevents-spigot:${packeteventsSpigotVersion}"
    compileOnly "net.kyori:adventure-text-minimessage:${adventureTextMinimessageVersion}"
    compileOnly 'io.th0rgal:oraxen:1.174.0'
    compileOnly("io.th0rgal:oraxen:2.0-SNAPSHOT")
    compileOnly("com.nexomc:nexo:${nexoVersion}")
    compileOnly("com.viaversion:viaversion-api:${viaVersionApiVersion}") {
        exclude group: 'io.papermc.paper' // Exclude Paper API
    }
    compileOnly "net.kyori:adventure-text-serializer-legacy:${adventureTextSerializerLegacyVersion}"
    compileOnly "net.kyori:adventure-text-minimessage:${adventureTextMinimessageVersion}"
    compileOnly "net.jodah:expiringmap:${expiringMapVersion}"
    compileOnly "org.apache.commons:commons-jexl3:${commonsJexl3Version}"
    compileOnly("org.geysermc.floodgate:api:${floodgateApiVersion}")
    compileOnly("org.geysermc.geyser:api:${geyserApiVersion}")


    implementation "org.bstats:bstats-bukkit:${bstatsBukkitVersion}"
    implementation "org.jetbrains:annotations:${annotationsVersion}"
    implementation "de.themoep:minedown-adventure:${minedownAdventureVersion}"
    implementation "com.github.alexdev03:drink:${drinkVersion}"
    implementation "me.tofaa.entitylib:spigot:${EntityLibVersion}"
    implementation "com.github.Anon8281:UniversalScheduler:${universalSchedulerVersion}"
    implementation "net.byteflux:libby-bukkit:${libbyBukkitVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

shadowJar {
    relocate 'org.jetbrains', relocator + 'jetbrains'
    relocate 'org.intellij', relocator + 'intellij'
    relocate 'de.themoep', relocator + 'themoep'
    relocate 'me.tofaa.entitylib', relocator + 'entitylib'
    relocate 'com.jonahseguin.drink', relocator + 'drink'
    relocate 'javax.annotation', relocator + 'annotation'
    relocate 'com.github.Anon8281.universalScheduler', relocator + 'universalScheduler'
    relocate 'net.kyori.adventure.text.serializer', 'io.github.retrooper.packetevents.adventure.serializer'
    relocate 'net.byteflux.libby', relocator + 'libby.bukkit'
    relocate 'org.bstats', relocator + 'bstats'


    dependencies {
        exclude dependency(':slf4j-api')
        exclude dependency('com.google.code.gson:gson')
        exclude dependency('net.kyori:adventure-api')
        exclude dependency('net.kyori:adventure-key')
        exclude dependency('net.kyori:adventure-nbt')
        exclude dependency('net.kyori:option')
        exclude dependency('net.kyori:examination-api')
        exclude dependency('net.kyori:examination-string')
        exclude dependency('net.kyori:text')
        exclude dependency('net.kyori:adventure-text-serializer-gson')
        exclude dependency('net.kyori:adventure-text-serializer-json')
    }


    //mappings
    exclude 'assets/mappings/block/**'
    exclude 'assets/mappings/stats/**'
    exclude 'assets/mappings/particle/**'
    exclude 'assets/mappings/enchantment/**'

    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set(null)
    archiveFileName.set("$project.name" + ".jar")

    minimize() {
    }
}

tasks {
    runServer {
        minecraftVersion("1.21.1")

        downloadPlugins {
            hangar("PlaceholderAPI", "2.11.6")
//            url("https://cdn.modrinth.com/data/HQyibRsN/versions/J2guR3GH/MiniPlaceholders-Paper-2.2.4.jar")
//            url("https://github.com/MiniPlaceholders/Player-Expansion/releases/download/1.2.0/MiniPlaceholders-Player-Expansion-1.2.0.jar")
            url("https://download.luckperms.net/1556/bukkit/loader/LuckPerms-Bukkit-5.4.141.jar")
            url("https://github.com/retrooper/packetevents/releases/download/v2.6.0/packetevents-spigot-2.6.0.jar")
//            url("https://github.com/IntellectualSites/FastAsyncWorldEdit/releases/download/2.11.2/FastAsyncWorldEdit-Bukkit-2.11.2.jar")
            github("ViaVersion", "ViaVersion", "5.1.1", "ViaVersion-5.1.1.jar")
            github("ViaVersion", "ViaBackwards", "5.1.1", "ViaBackwards-5.1.1.jar")
            github("MilkBowl", "Vault", "1.7.3", "Vault.jar")
            github("gecolay", "GSit", "1.11.2", "GSit-1.11.2.jar")
            url("https://github.com/EssentialsX/Essentials/releases/download/2.20.1/EssentialsX-2.20.1.jar")
        }
    }
    runPaper.folia.registerTask() {
        minecraftVersion("1.21.1")

        downloadPlugins {
            github("Anon8281", "PlaceholderAPI", "2.11.7", "PlaceholderAPI-2.11.7-DEV-Folia.jar")

            url("https://cdn.modrinth.com/data/HQyibRsN/versions/J2guR3GH/MiniPlaceholders-Paper-2.2.4.jar")
            url("https://ci.lucko.me/job/LuckPerms-Folia/lastSuccessfulBuild/artifact/bukkit/loader/build/libs/LuckPerms-Bukkit-5.4.141.jar")
            url("https://github.com/retrooper/packetevents/releases/download/v2.4.0/packetevents-spigot-2.4.0.jar")
            github("ViaVersion", "ViaVersion", "5.0.1", "ViaVersion-5.0.1.jar")
        }
    }
}


build.dependsOn shadowJar
clean.delete "$rootDir/target"

def targetJavaVersion = 17
java {
    sourceCompatibility = targetJavaVersion
    targetCompatibility = 17

    disableAutoTargetJvm()
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.register('deleteClassFiles', Delete) {
    delete fileTree(directory: 'build/classes/java/main').include('**/*_1_17.class')
}


processResources {
    def props = project.properties
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
