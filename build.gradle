plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.7"
    id 'maven-publish'
    id 'xyz.jpenilla.run-paper' version '2.3.0'
}

group = 'com.github.alexdev03'

repositories {
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven { url = 'https://repo.minebench.de/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://repo.opencollab.dev/maven-public/' }
    maven { url = 'https://repo.codemc.io/repository/maven-releases/' }
    maven { url = 'https://repo.viaversion.com/' }
    maven { url = 'https://repo.opencollab.dev/main/' }
    mavenLocal()
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
    maven { url = "https://repo.oraxen.com/releases" }
}

dependencies {
    compileOnly "com.github.gabber235:typewriter:${typeWriterVersion}"
    compileOnly "net.kyori:adventure-api:${adventureApiVersion}"
    compileOnly "io.papermc.paper:paper-api:${paperApiVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "me.clip:placeholderapi:${placeholderapiVersion}"
    compileOnly "io.github.miniplaceholders:miniplaceholders-api:${miniplaceholdersApiVersion}"
    compileOnly "net.kyori:adventure-api:${adventureApiVersion}"
    compileOnly "org.geysermc.floodgate:api:${floodgateApiVersion}"
    compileOnly "de.exlll:configlib-yaml:${configlibVersion}"
    compileOnly "de.exlll:configlib-paper:${configlibVersion}"
    compileOnly "com.github.retrooper.packetevents:spigot:${packeteventsSpigotVersion}"
    compileOnly "net.kyori:adventure-text-minimessage:${adventureTextMinimessageVersion}"
    compileOnly 'io.th0rgal:oraxen:1.174.0'

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.jetbrains:annotations:${annotationsVersion}"
    implementation "de.themoep:minedown-adventure:${minedownAdventureVersion}"
    implementation "com.github.alexdev03:drink:${drinkVersion}"
    implementation "com.github.Tofaa2.EntityLib:spigot:${EntityLibVersion}"
    implementation "com.github.Anon8281:UniversalScheduler:${universalSchedulerVersion}"
    implementation "com.alessiodp.libby:libby-bukkit:${libbyVersion}"
    implementation "net.kyori:adventure-text-serializer-legacy:4.17.0"
    implementation "net.kyori:adventure-text-minimessage:4.17.0"
}


shadowJar {
    relocate 'org.jetbrains', relocator + 'jetbrains'
    relocate 'org.intellij', relocator + 'intellij'
    relocate 'de.themoep', relocator + 'themoep'
    relocate 'me.tofaa.entitylib', relocator + 'entitylib'
    relocate 'com.jonahseguin.drink', relocator + 'drink'
    relocate 'javax.annotation', relocator + 'annotation'
    relocate 'com.github.Anon8281.universalScheduler', relocator + 'universalScheduler'
    relocate 'com.alessiodp.libby', relocator + 'libby'

    relocate 'net.kyori.adventure.text.serializer.legacy', relocator + 'adventure.text.serializer.legacy'
    relocate 'net.kyori.adventure.text.minimessage', relocator + 'adventure.text.minimessage'

    dependencies {
        exclude dependency(':slf4j-api')
        exclude dependency('com.google.code.gson:gson')
        exclude dependency('net.kyori:adventure-api')
        exclude dependency('net.kyori:adventure-key')
        exclude dependency('net.kyori:adventure-nbt')
        exclude dependency('net.kyori:option')
        exclude dependency('net.kyori:examination-api')
        exclude dependency('net.kyori:examination-string')
        exclude dependency('net.kyori:text')
        exclude dependency('net.kyori:adventure-text-serializer-gson')
//        exclude dependency('net.kyori:adventure-text-serializer-legacy')
        exclude dependency('net.kyori:adventure-text-serializer-json')
    }

//    relocate(
//            "net.kyori.adventure.text.serializer.legacy",
//            "io.github.retrooper.packetevents.adventure.serializer.legacy"
//    )

    //mappings
    exclude 'assets/mappings/block/**'
    exclude 'assets/mappings/stats/**'
    exclude 'assets/mappings/particle/**'
    exclude 'assets/mappings/enchantment/**'

    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set(null)
    archiveFileName.set("$project.name" + ".jar")

    minimize()
}

tasks {
    runServer {
        minecraftVersion("1.20.6")

        downloadPlugins {
            hangar("PlaceholderAPI", "2.11.6")
            url("https://cdn.modrinth.com/data/HQyibRsN/versions/J2guR3GH/MiniPlaceholders-Paper-2.2.4.jar")
            url("https://download.luckperms.net/1539/bukkit/loader/LuckPerms-Bukkit-5.4.126.jar")
            url("https://github.com/retrooper/packetevents/releases/download/v2.3.0/packetevents-spigot-2.3.0.jar")
        }
    }
    runPaper.folia.registerTask() {
        minecraftVersion("1.20.6")

        downloadPlugins {
            github("Anon8281", "PlaceholderAPI", "2.11.7", "PlaceholderAPI-2.11.7-DEV-Folia.jar")
            url("https://cdn.modrinth.com/data/HQyibRsN/versions/J2guR3GH/MiniPlaceholders-Paper-2.2.4.jar")
            url("https://ci.lucko.me/job/LuckPerms-Folia/lastSuccessfulBuild/artifact/bukkit/loader/build/libs/LuckPerms-Bukkit-5.4.117.jar")
            url("https://github.com/retrooper/packetevents/releases/download/v2.3.0/packetevents-spigot-2.3.0.jar")
        }
    }
}


build.dependsOn shadowJar
clean.delete "$rootDir/target"

def targetJavaVersion = 17
java {
    sourceCompatibility = targetJavaVersion
    targetCompatibility = 21
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.register('deleteClassFiles', Delete) {
    delete fileTree(directory: 'build/classes/java/main').include('**/*_1_17.class')
}


processResources {
    def props = project.properties
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
